%lex
D	[0-9]
C	[A-Za-z_]
COLOR	"#"[A-Fa-f0-9]{6}
%%

\s+		{ /* skip WS */}
"group"		{return 'GROUP';}
"start"		{return 'START';}
">"		{return 'EVENT';}
{COLOR}		{return 'COLOR';}
{C}+{D}*	{return 'STATE';}
{D}+		{return 'NUMBER';}
":".*		{return 'LABEL';}
<<EOF>>		{return 'EOF';}

/lex
%start S

%%

S
	: contents EOF
		{
			console.log("digraph {");
			console.log($1); 
			console.log("}");
			return $1;
		}
	;

contents
	: content
		{$$ = $1;}
	| contents content
		{$$ =  $1 + $2;}
	;

/* process all STATE EVENT .* possibilities */
targetState
	: COLOR STATE LABEL
		{$$=$2+"[color=\""+$1+"\",label=\""+$3+"\"];\n";}
	| STATE LABEL
		{$$=$1+"[label=\""+$2+"\"];\n";}
	| COLOR STATE
		{$$=$2+"[color=\""+$1+"\"];\n";}
	| STATE
		{$$=$1+";\n";}
	;
		
/* process all STATE .* possibilities*/
colorLabel
	: COLOR LABEL
		{$$="[color=\""+$1+"\",label=\""+$2+"\"];\n"}
	| LABEL
		{$$="[label=\""+$1+"\"];\n"}
	;

content
	: GROUP NUMBER
		{{$$="subgraph cluster_"+$2+"{\n";}}
	| GROUP NUMBER LABEL
		{{$$="subgraph cluster_"+$2+"{\nlabel=\""+$3+"\"";}}
	| GROUP STATE
		{{$$="};\n";}}
	| START STATE
		{$$="{rank = same;null}\n{rank = same; "+$2+"}\nnull [shape=plaintext, label=\"\"];\n"+$2+"[shape=doublecircle];\nnull->"+$2+";\n";}
	| STATE EVENT targetState
		{$$=$1+"->"+$3;}
	| STATE colorLabel
		{$$=$1+$2;}
	| EVENT STATE
		{$$="->"+$2;}
	;
