visualizer dot

$(red:#ff0000)
$(blue:#0000ff)
$(yellow:#ffff00)
 
shape rect
closed$(red);CLOSED
closed_end$(red);closed\nBack to start
listen$(yellow);LISTEN
syn_received$(grey:#888888);SYN\nRECEIVED
syn_sent$(grey);SYN\nSENT
established#00ff00;ESTABLISHED\ndata xchange occurs

{ PassiveClose$(lightblue:#afeeee);Passive CLOSE
  close_wait$(yellow);CLOSE WAIT
  last_ack$(yellow);LAST ACK
}

{ ActiveClose$(lightblue);Active CLOSE
  fin_wait1$(yellow);FIN WAIT 1
  closing$(yellow);CLOSING
  fin_wait2$(yellow);FIN WAIT 2
  time_wait$(yellow);TIME WAIT
}

//unusual event
syn_sent.>closed;CLOSE/-
syn_sent.>syn_received;SYN/SYN+ACK\n(simultaneous open)
listen.>syn_sent;SEND/SYN
listen.>closed;CLOSE/-
syn_received.>listen;RST/-
syn_received.>"CLOSE/FIN"fin_wait1
	.>"FIN/ACK"closing
fin_wait1.>time_wait;FIN+ACK/ACK
closing.>time_wait

// client / receiver path
closed>$(red)"CONNECT/SYN(1/3 of 3w/hs)"syn_sent
	>$(red)"SYN+ACK/ACK(3/3 of 3w/hs)"established
	>$(red)"CLOSE/FIN"fin_wait1
	>$(red)"ACK/-"fin_wait2
	>$(red)"FIN/ACK"time_wait
	>$(red)closed_end;Timeout

// server / sender path
closed>$(blue)"LISTEN/-"listen
	>$(blue)"SYN/SYN+ACK(2/3 of 3w/hs)"syn_received
	>$(blue)"ACK/-"established
	>$(blue)"FIN/ACK"close_wait
	>$(blue)"CLOSE/FIN"last_ack
	>$(blue)closed_end

